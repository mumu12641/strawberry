use crate::grammar::ast::class::*;
use crate::grammar::ast::expr::*;
use crate::grammar::token::Token;
use crate::grammar::lexer::{LineNum, LexicalError};

grammar<'input>;

// * => zero or more
// + => once or more
// ? => zero or once
pub Program = <import*>  <class*>;

import: Import = {
    "FROM" <s:"STR"> "IMPORT" <class_name_:"TYPE"> ";" => Import{
        file_name:s,
        class_name:class_name_,
    },
};

class: Class = {
    <l:"CLASS"> <class_name:"TYPE"> "{" <f:feature*> "}" ";" => Class {
        name: class_name,
        parent: None,
        features: f,
        position: l.0,
        file_name: l.1,
    },
    <l:"CLASS"> <class_name:"TYPE"> "INHERITS" <parent_name:"TYPE"> "{" <f:feature*> "}" ";" => Class {
        name: class_name,
        parent: Some(parent_name),
        features: f,
        position: l.0,
        file_name: l.1,
    },
};

feature: Feature = {
    // attr
    "private"? <attr_name:"ID"> ":" <type_name:"TYPE"> ";" => {
        Feature::Attribute(VarDecl {
            name: attr_name.0,
            type_: Some(type_name),
            init: Box::new(None),
            position: attr_name.1,
            ownership: Ownership::Private,
        })
    },
    "public" <attr_name:"ID"> ":" <type_name:"TYPE"> ";" => {
        Feature::Attribute(VarDecl {
            name: attr_name.0,
            type_: Some(type_name),
            init: Box::new(None),
            position: attr_name.1,
            ownership: Ownership::Public,
        })
    },

    "private"? <attr_name:"ID"> ":" <type_name:"TYPE"> "=" <e:single_expr> ";" => {
        Feature::Attribute(VarDecl{
            name:attr_name.0,
            type_:Some(type_name),
            init:Box::new(Some(e)),
            position: attr_name.1,
            ownership: Ownership::Private,
        })
    },
    "public" <attr_name:"ID"> ":" <type_name:"TYPE"> "=" <e:single_expr> ";" => {
        Feature::Attribute(VarDecl{
            name:attr_name.0,
            type_:Some(type_name),
            init:Box::new(Some(e)),
            position: attr_name.1,
            ownership: Ownership::Public,
        })
    },


    
    // function
    "private"? <pos:"FUN"> <method_name:"ID"> "(" <param_:comma_list<param>?>  ")" "->" <r_type:"TYPE"> "{" <e:semi_list<expr>>  "}" ";" => {
        Feature::Method(MethodDecl{
            name: method_name.0,
            param: Box::new(param_.unwrap_or(vec![])),
            return_type:r_type,
            body: Box::new(Some(e)),
            position: pos,
            ownership: Ownership::Private,
            // body: Box::new(Box::new(e)),
        })
    },
    "public" <pos:"FUN"> <method_name:"ID"> "(" <param_:comma_list<param>?>  ")" "->" <r_type:"TYPE"> "{" <e:semi_list<expr>>  "}" ";" => {
        Feature::Method(MethodDecl{
            name: method_name.0,
            param: Box::new(param_.unwrap_or(vec![])),
            return_type:r_type,
            body: Box::new(Some(e)),
            position: pos,
            ownership: Ownership::Public,
            // body: Box::new(Box::new(e)),
        })
    },

    "private"? <pos: "FUN"> <method_name:"ID"> "(" <param_:comma_list<param>?> ")" "->" <r_type:"TYPE"> "{"   "}"  ";"=> {
        Feature::Method(MethodDecl{
            name: method_name.0,
            param: Box::new(param_.unwrap_or(vec![])),
            return_type:r_type,
            body: Box::new(None),
            position: pos,
            ownership: Ownership::Private,
        })
    },
    "public" <pos: "FUN"> <method_name:"ID"> "(" <param_:comma_list<param>?> ")" "->" <r_type:"TYPE"> "{"   "}"  ";"=> {
        Feature::Method(MethodDecl{
            name: method_name.0,
            param: Box::new(param_.unwrap_or(vec![])),
            return_type:r_type,
            body: Box::new(None),
            position: pos,
            ownership: Ownership::Public,
        })
    },

};

param: ParamDecl = {
    <id:"ID"> ":" <t:"TYPE"> => (id.0,t),
};

// let var
var: VarDecl = {
    <id:"ID"> ":" <type_name:"TYPE"> => VarDecl{
        name:id.0,
        type_: Some(type_name),
        init: Box::new(None),
        position: id.1,
        ownership: Ownership::Default,
    },
    <id:"ID"> ":" <type_name:"TYPE"> "=" <e:math_expr> => VarDecl{
        name:id.0,
        type_: Some(type_name),
        init: Box::new(Some(e)),
        position: id.1,
        ownership: Ownership::Default,
    },

    <id:"ID">  "=" <e:math_expr> => VarDecl{
        name:id.0,
        type_: None,
        init: Box::new(Some(e)),
        position: id.1,
        ownership: Ownership::Default,
    },
};

let_expr:Expr = {
    "LET" <var_:comma_list<var>> ";"=> Expr::Let(Let{var_decls:Box::new(var_)}),
};

assign_expr:Expr = {
    <id:"ID"> "=" <expr :math_expr>";" => Expr::Assignment(Assignment{id:id.0, compute:Box::new(expr),position:id.1}),
};

// let assign if while 
expr: Expr = {
    
    

    <e:single_expr> "." <id:"ID"> "(" <args:comma_list<math_expr>?> ")" ";" => {
        Expr::Dispatch (Dispatch{
            target: Box::new(e),
            // fun_name: id.0,
            // actual: Box::new(args.unwrap_or(vec![])),
            expr: DispatchExpr::Method(MethodCall{
                fun_name: id.0,
                actual: Box::new(args.unwrap_or(vec![])),
            }),
            position: id.1,
            type_: "Object".to_string()
        })
    },
    
    


    <id:"ID"> "(" <args:comma_list<math_expr>?>  ")"  ";"=> {
        Expr::Dispatch (Dispatch{
            target: Box::new(Expr::Self_(Self_{
                type_:"Object".to_string()
            }
            )),
            // fun_name: id.0,
            // actual: Box::new(args.unwrap_or(vec![])),
            expr: DispatchExpr::Method(MethodCall{
                fun_name: id.0,
                actual: Box::new(args.unwrap_or(vec![])),
            }),
            position: id.1,
            type_: "Object".to_string()
        })
    },

    <pos: "IF"> "(" <test_:cond_expr> ")"   "{" <then_:expr*> "}" "ELSE" "{" <else_:expr*> "}"  => Expr::Cond(Cond{
        test:Box::new(test_),
        then_body: Box::new(then_),
        else_body: Box::new(else_),
        position: pos,
    }),

    <pos: "IF"> <test_:cond_expr>   "{" <then_:expr*> "}" "ELSE" "{" <else_:expr*> "}"  => Expr::Cond(Cond{
        test:Box::new(test_),
        then_body: Box::new(then_),
        else_body: Box::new(else_),
        position: pos,
    }),

    <pos:"WHILE">  "(" <test_:cond_expr> ")" "{" <body_:expr*> "}" => Expr::While(While{
        test:Box::new(test_),
        body: Box::new(body_),
        position: pos,
    }),

    <pos: "WHILE"> <test_:cond_expr> "{" <body_:expr*> "}" => Expr::While(While{
        test:Box::new(test_),
        body: Box::new(body_),
        position: pos,
    }),

    <pos: "FOR"> "(" <init_:let_expr*>  <test_:cond_expr*> ";" <iter_:comma_list<expr>> ")" "{" <body_:expr*> "}" => Expr::For(For{
        init: Box::new(init_),
        test: Box::new(test_),
        iter: Box::new(iter_),
        body: Box::new(body_),
        position: pos,
    }),

    <pos: "RETURN">  <val:math_expr> ";"=> Expr::Return(Return{
        val: Some(Box::new(val)),
        position: pos,
    }),
<pos: "RETURN"> ";"=> Expr::Return(Return{
        val: None,
        position: pos,
    }),


    math_expr,
    let_expr,
    assign_expr,
};

single_expr:Expr = {
    <id:"ID"> => Expr::Identifier(IdentifierSrtuct {
        name: id.0,
        pos: id.1,
        type_: "Object".to_string()
    }),
    <s:"STR"> => Expr::Str(s),
    <e:"SELF"> => Expr::Self_(Self_{
        type_:"Object".to_string()
    }),
    <b:"BOOL"> => Expr::Bool(b),
    <i:"INT"> => Expr::Int(i.parse::<i32>().unwrap()), 
    // <s:"SELF"> => Expr::Self_(s),
    
    "NEW" <class_name:"TYPE"> => Expr::New(<>), 

    <id:"ID"> "(" <args:comma_list<math_expr>?>  ")" => {
        Expr::Dispatch (Dispatch{
            // target: Box::new(Some("self".to_string())),
            // target:Box::new(Some(Expr::Identifier(
            //     IdentifierSrtuct{
            //         name: "self".to_string(),
            //         pos:(0,0),
            //         type_:"Object".to_string()
            //     }))),
            target: Box::new(Expr::Self_(Self_{
                type_:"Object".to_string()
            }
            )),
            // fun_name: id.0,
            // actual: Box::new(args.unwrap_or(vec![])),
            expr: DispatchExpr::Method(MethodCall{
                fun_name: id.0,
                actual: Box::new(args.unwrap_or(vec![])),
            }),
            position: id.1,
            type_: "Object".to_string()
        })
    },
    
    <e:single_expr> "." <id:"ID"> "(" <args:comma_list<math_expr>?> ")" => {
        Expr::Dispatch (Dispatch{
            target: Box::new(e),
            // fun_name: id.0,
            // actual: Box::new(args.unwrap_or(vec![])),
            expr: DispatchExpr::Method(MethodCall{
                fun_name: id.0,
                actual: Box::new(args.unwrap_or(vec![])),
            }),
            position: id.1,
            type_: "Object".to_string()
        })
    },

    <e:single_expr> "." <id:"ID"> =>{
        Expr::Dispatch (Dispatch{
            target: Box::new(e),
            expr: DispatchExpr::Field(id.0),
            position: id.1,
            type_: "Object".to_string()
        })
    }

};


add_minus: MathOp = {
    "+" => MathOp::ComputeOp(ComputeOp::Add),
    "-" => MathOp::ComputeOp(ComputeOp::Minus),
};

mul_div: MathOp = {
    "/" => MathOp::ComputeOp(ComputeOp::Divide),
    "*" => MathOp::ComputeOp(ComputeOp::Mul),
};

cond_op: MathOp = {
    ">" => MathOp::CondOp(CondOp::More),
    "=>" => MathOp::CondOp(CondOp::MoreE),
    "<" => MathOp::CondOp(CondOp::Less),
    "<=" => MathOp::CondOp(CondOp::LessE),
    "==" => MathOp::CondOp(CondOp::Equal),
    // "=>" => MathOp::CondOp::MoreE,
    // "<" => MathOp::CondOp::Less,
    // "<=" => MathOp::CondOp::LessE,
    // "==" => MathOp::CondOp::Equal,
}

math_term:Expr ={
    <l:math_term> <op:mul_div> <r:single_expr> => Expr::Math(Math {
        left: Box::new(l),
        op: Box::new(op),
        right: Box::new(r),
        type_: "Object".to_string(),
    }),
    single_expr,
};

math_expr: Expr = {
    <l:math_expr> <op:add_minus> <r:math_term> => Expr::Math(Math {
        left: Box::new(l),
        op: Box::new(op),
        right: Box::new(r),
        type_: "Object".to_string(),
    }),
    math_term,
};

cond_expr: Expr = {
    <l:math_expr> <op:cond_op> <r:math_expr> => Expr::Math(Math { 
        left: Box::new(l),
        op: Box::new(op),
        right: Box::new(r),
        type_: "Object".to_string(),
    }),
    <pos:"!"> <e:cond_expr> => Expr::Not(Not{
        expr:Box::new(e),
        position: pos,
    }),
    "ISNULL" "(" <e:single_expr> ")" => Expr::Isnull(Isnull{
        expr:Box::new(e),
    }),
    math_expr,
}


#[inline]
comma_list<T>: Vec<T> = {
    <i1: T> <i2:("," T)*> => {
        let mut items = vec![i1];
        items.extend(i2.into_iter().map(|e| e.1));
        items
    }
};

#[inline]
semi_list<T>:Vec<T> = {
    // <i1: T> <i2:(";" T)*> => {
        <i1: T> <i2: T*> => {
        let mut items = vec![i1];
        items.extend(i2.into_iter().map(|e| e));
        items
    }
}


extern {
    type Location = LineNum;
    type Error = LexicalError;
    // type Pos = Position;

    // Map parser terms to token values:
    enum Token{
        "CLASS" => Token::Class_(<(usize,usize)>,<String>),
        "public" => Token::Public,
        "private" => Token::Private,

        // "SELF" => Token::Self_,
        "FUN" => Token::Function(<(usize,usize)>),
        "RETURN" => Token::Return(<(usize,usize)>),
        "IF" => Token::If(<(usize,usize)>),
        "THEN" => Token::Then,
        "ELSE" => Token::Else(<(usize,usize)>),
        "INHERITS" => Token::Inherits,
        "LET" => Token::Let(<(usize,usize)>),
        "WHILE" => Token::While(<(usize,usize)>),
        "FOR" => Token::For(<(usize, usize)>),
        "NEW" => Token::New(<(usize,usize)>),
        "ISNULL" => Token::Isnull,
        "SELF" => Token::Self_(<String>),
        "!" => Token::Not(<(usize,usize)>),
        "->" => Token::Arrow,

        "STR" => Token::StringConst(<String>),
        "INT" => Token::IntConst(<String>),
        "BOOL" => Token::BoolConst(<bool>),
        "TYPE" => Token::TypeId(<String>),
        "ID" => Token::Identifier(<String>,<(usize,usize)>),

        "=" => Token::Assign(<(usize,usize)>),
        "+" => Token::Plus,
        "-" => Token::Minus,
        "/" => Token::Divide,
        "*" => Token::Mul,
        "==" => Token::Equal,
        ">" => Token::More,
        "=>" => Token::MoreE,
        "<" => Token::Less,
        "<=" => Token::LessE,
        
        "{" => Token::Lbrace,
        "}" => Token::Rbrace,
        "(" => Token::Lparen,
        ")" => Token::Rparen,
        ";" =>Token::Semicolon,
        "." => Token::Period,
        "," => Token::Comma,
        ":" =>Token::Colon,

        "IMPORT" => Token::Import,
        "FROM" => Token::From,

        "ERROR" => Token::Error(<String>),
    }

}

